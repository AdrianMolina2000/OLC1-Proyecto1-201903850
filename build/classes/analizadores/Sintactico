/*
 * Analizador Sintactico
 * Adrian Samuel Molina Cabrera
 * 201930850
 */

package analizadores; 


import java_cup.runtime.*;
import java.util.LinkedList;
import estructuras.ast;
import estructuras.nodo;

parser code 
{:

    public LinkedList<ast> arboles = new LinkedList<>();
    public int contador = 0;

    
    /**
     * Método al que se llama automáticamente ante algún error sintactico.
     **/ 
    public void syntax_error(Symbol s){ 
        System.out.println("Error Sintáctico en la Línea " + (s.left) +
        " Columna "+s.right+ ". No se esperaba este componente: " +s.value+"."); 
    } 

    /**
     * Método al que se llama automáticamente ante algún error sintáctico 
     * en el que ya no es posible una recuperación de errores.
     **/ 
    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{ 
        System.out.println("Error síntactico irrecuperable en la Línea " + 
        (s.left)+ " Columna "+s.right+". Componente " + s.value + 
        " no reconocido."); 
    }  

    public LinkedList<ast> getAST() {
        return arboles;
    }
:} 

terminal String DPUNTO, ASI, GNAR, RANGO, PTCOMA, COMA, LLAIZQ, LLADER;
terminal String CONCAT, CEROMAS, UNOMAS, CERUNO, DISYUNCION, SEPARADOR;
terminal String ENTERO, LOWER, UPPER, LOPPER, ASCII, LEXEM,IDENTIFICADOR, ALFA, COMILLADOBLE, DIAG;
terminal String CONJUNTO, SALTO, CARACTER;

non terminal ini;
non terminal instrucciones, listaInstrucciones, listaLexemas, instruccion;
non terminal conjuntos, listaL, listaN, lexemas;
non terminal nodo expresion, hoja;

precedence right CONCAT, DISYUNCION, CEROMAS, UNOMAS, CERUNO, COMA;
precedence left LLAIZQ, LLADER,CEROMAS, IDENTIFICADOR;

start with ini; 
ini::=
     instrucciones
    |error instrucciones
    |error
;
  
instrucciones ::=
    LLAIZQ listaInstrucciones LLADER instrucciones
   |LLAIZQ listaInstrucciones LLADER 
;

listaInstrucciones::=
    listaInstrucciones instruccion
   |instruccion
;

instruccion::=
    CONJUNTO DPUNTO IDENTIFICADOR:a ASI GNAR conjuntos PTCOMA
   |IDENTIFICADOR:a ASI GNAR expresion:b {:arboles.add(new ast(a, new nodo(".", b, new nodo("#", contador++, "especial"), contador++)));:} PTCOMA
   |SEPARADOR SEPARADOR listaLexemas
   
;

conjuntos::=
    LOWER:b RANGO LOWER:c
   |UPPER:b RANGO UPPER:c
   |ASCII RANGO ASCII
   |ENTERO:b RANGO ENTERO:c 
   |LOWER:b  COMA listaL 
   |UPPER:b  COMA listaL 
   |ENTERO:b COMA listaN 
;

listaL::=
   |LOWER:d COMA listaL
   |UPPER:d COMA listaL
   |LOWER:d
   |UPPER:d 
;

listaN::=
   |ENTERO:d COMA listaN
   |ENTERO:d 
;

expresion::=
    DISYUNCION hoja:a hoja:b            {:RESULT = new nodo("|", a, b, contador++);:}
   |DISYUNCION hoja:a expresion:b       {:RESULT = new nodo("|", b, a, contador++);:}
   |DISYUNCION expresion:a expresion:b  {:RESULT = new nodo("|", a, b, contador++);:}
   |DISYUNCION expresion:a hoja:b       {:RESULT = new nodo("|", a, b, contador++);:}
   |CONCAT hoja:a hoja:b                {:RESULT = new nodo(".", a, b, contador++);:}
   |CONCAT hoja:a expresion:b           {:RESULT = new nodo(".", a, b, contador++);:}
   |CONCAT expresion:a expresion:b      {:RESULT = new nodo(".", a, b, contador++);:}
   |CONCAT expresion:a hoja:b           {:RESULT = new nodo(".", b, a, contador++);:}
   |UNOMAS hoja:a                       {:RESULT = new nodo("+", a, null, contador++);:}
   |UNOMAS expresion:a                  {:RESULT = new nodo("+", a, null, contador++);:}
   |CEROMAS hoja:a                      {:RESULT = new nodo("*", a, null, contador++);:}
   |CEROMAS expresion:a                 {:RESULT = new nodo("*", a, null, contador++);:}
   |CERUNO hoja:a                       {:RESULT = new nodo("?", a, null, contador++);:}
   |CERUNO expresion:a                  {:RESULT = new nodo("?", a, null, contador++);:}
;

hoja::=
    ALFA:a          {:RESULT = new nodo(a, contador++, "conjunto");:}
   |CARACTER:a      {:RESULT = new nodo(a, contador++, "caracter");:}
   |SALTO:a         {:RESULT = new nodo(a, contador++, "especial");:}
   |DIAG:a          {:RESULT = new nodo(a, contador++, "especial");:}
;

listaLexemas::=
    listaLexemas lexemas
   |lexemas
;

lexemas::=
   IDENTIFICADOR:a DPUNTO LEXEM:b PTCOMA
;

